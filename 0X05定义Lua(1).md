# 0X05 定义 Lua (1)

Lua 在官方的 Document 中给出了其语法的定义.在此我将介绍这些定义,练习 EBNF 顺便复习 Lua.

其实 Lua 的 EBNF 定义不太完全,其中的一些名词是通过自然语言描述的.我将尽量把这些内容用形式化的 EBNF 定义出来.对于不太符合我们刚刚介绍的 ISO 标准的写法我也会加以改写.

由于介绍 EBNF 自定义的时候我一定程度上采用了自底向上的构造性的顺序,这里我用一种不同的顺序--自顶向下.

## syntax

由于 Lua 语言以 chunk 为编译单位.这里我定义 Lua 的 syntax 等价于 chunk.

```EBNF
syntax = chunk;
```

## chunk和block

```EBNF
chunk = block;
block = {stat}, [retstat];
```

这里我们又用到了两个没有定义过的名词'stat'和'retstat'.它们就是指我们很熟悉的 Lua 语句和 return 语句.

## 语句

```EBNF
stat = ';' |
    varlist, '=', explist |
    functioncall |
    label |
    'break' |
    'goto', Name |
    'do', block, 'end' |
    'while', exp, 'do', block, 'end' |
    'repeat', block, 'until', exp |
    'if', exp, 'then', block, {'elseif', exp, 'then', block}, ['else', block], 'end' |
    'for', Name, '=', exp, ',', exp, [',', exp], 'do', block, 'end' |
    'for', namelist, 'in', explist, 'do', block, 'end' |
    'function', funcname, funcbody |
    'local', 'function', Name, funcbody |
    'local', namelist, ['=', explist];
```

这个语句非常长,但是实际就是说明'stat'表示任意一条 Lua 中的语句.顺便定义了一些语句的结构.从上到下分别是:

1. 空语句
1. 赋值语句
1. 函数调用
1. 标签
1. break 语句
1. goto 语句
1. do-end 语句
1. 当型循环语句
1. 直到型循环语句
1. 选择分支语句
1. 计数型 for 循环
1. 遍历型 for 循环
1. 函数定义语句
1. 带局部声明的函数定义语句(定义的函数名为局部变量)
1. 局部变量声明和初始化语句

其中又多出来很多的未定义词汇.我们将在后边逐个进行定义.

注意这些语句的定义中,有的直接出现了 block,体现了语句之间可以嵌套.

上述的语句中并没有出现 retstat,所以单独的 retstat 并不是 stat.它必须在一个 block 中使用.

现在我们定义 retstat:

```EBNF
retstat = 'return', [explist], [';'];
```

## 标签

以下是对标签语句的定义:

```EBNF
label = '::', Name, '::';
```

## 函数的定义和调用

首先是函数名:

```EBNF
funcname = Name, {'.', Name}, [':', Name];
```

则函数定义语句如下:

```EBNF
functiondef = 'function', funcbody;
```

这里给出的定义方式是匿名函数的定义方式.它本身是一个表达式.之前 stat 的定义中出现的两种定义则是明明函数的定义方式.注意 local 和 function 连用的时候,不能把函数命名为 funcname 格式,而应该是 Name 格式.

然后是函数体的定义:

```EBNF
funcbody = '(', [parlist], ')', block, end;
```

其中的 parlist 包含了可变参数个数的写法.其定义如下:

```EBNF
parlist = namelist, [',', '...'] | '...';
```

函数调用的语句如下:

```EBNF
functioncall = prefixexp, args | prefixexp, ':', Name, args;
```

注意这里定义的时候没有用 funcname 定义 functioncall.因为函数不一定都有名字,有时候调用的是匿名函数.

函数调用中的 args 定义如下:

```EBNF
args = '(', [explist], ')' | tableconstructor | LiteralString;
```

## 各种列表

变量列表

```EBNF
varlist = var, {',', var};
```

表达式列表

```EBNF
explist = exp, {',', exp};
```

名称列表

```EBNF
namelist = Name, {',', Name};
```

## 变量和修饰表达式

变量的定义

```EBNF
var = Name | prefixexp, '[', exp, ']' | prefixexp, '.', Name;
```

这里的 prefixexp 在很多地方都出现了,它表示可以后加查表算符(方括号)或者成员运算(圆点)的内容.

```EBNF
prefixexp = var | functioncall | '(', exp, ')';
```
